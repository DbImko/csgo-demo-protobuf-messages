buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.3"
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: "com.google.protobuf"
apply plugin: com.github.dbimko.gradle.VersionPlugin
apply plugin: com.github.dbimko.gradle.JavaPlugin

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufJavaVersion}"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile "com.google.protobuf:protobuf-java:${protobufJavaVersion}"
}


jar {
    manifest {
        attributes(
                "Archiver-Version": "Gradle ${gradle.gradleVersion}",
                "Created-By": "Gradle",
                "Built-By": System.getProperty('user.name'),
                "Built-Jdk": System.getProperty('java.version'),
                "Component-Id": project.name,
                "Component-Version": version,
        )
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

bintray {
    user = System.getenv('bintray_user')
    key = System.getenv('bintray_api_key')
    publish = true
    pkg {
        publications = ['maven']
        repo = 'maven'
        userOrg = 'dbimko'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/DbImko/csgo-demo-protobuf-messages'
        vcsUrl = 'https://github.com/DbImko/csgo-demo-protobuf-messages'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

